:Class ejCheckBoxMedium : MiPageSample
⍝ Control:: _SF.ejCheckbox

⍝ Description:: 2 checkboxes with callbacks

    ∇ Compose;n
      :Access Public
    ⍝ We create an unchecked SyncFusion check box with caption 'Check Me!'
      cb1←'cb1'Add _.ejCheckBox 0 'Box 1'
     
      Add _.br
     
    ⍝ We create an unchecked SyncFusion check box with caption 'Check Me!'
      cb2←'cb2'Add _.ejCheckBox 0 'Set Box 1 as triState'
     
    ⍝ We tell the server to call program 'myCallback' any changes and to return
    ⍝ the value of the checkState variable in the model under the name 'state'
      (cb2 cb1).On⊂'change' 'myCallback'('state' 'model' 'checkState')
     
    ⍝ A place to write the message
      Add _.br
      'message'Add _.div
    ∇

    ∇ r←myCallback;st;i
      :Access public
      st←Get'state'
      :If _what≡'cb1'
        ⍝ Display the state of the check box in the message area
          r←'#message'Replace st
     
      :Else ⍝ must be cb2
        ⍝ Change the text of the check box
          i←1+st≡'check'
          'enableTriState'cb1.Set i⊃_false _true
          'checkState'cb1.Set i⊃'check' 'indeterminate'
          r←'#message'Replace''
          r,←'#cb2'Replace i⊃'Set Box 1 as triState' 'Unset Box 1 as triState'
     
      :EndIf
    ∇

:EndClass