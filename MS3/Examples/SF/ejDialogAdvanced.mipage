:Class ejDialogAdvanced : MiPageSample
⍝ Control:: _SF.ejDialogPicker
⍝ Description:: Two interacting dialogs

    ∇ Compose;dlg;content;ig
      :Access Public
      'onclick="$(''#dialog'').ejDialog(''open'')"'Add _.Button'Click here to re-open the dialog.'
     
      ⍝ Here is some content (see InputGridSimple for explanation):
      content←'dialog'Add _.div,New _.h2'Please tell us about yourself'
      content.Add _.form,ig←New _.InputGrid
      ig.(Border Labels)←0,⊂'First name' 'Last name' 'Birthdate'
      ig.Inputs←'fname' 'lname' 'bdate'New¨_.EditField _.EditField _.jqDatePicker
      ig.Inputs[3].Set'dateFormat' 'dd/mm/yy'
      content.Add'<big>WHACK ME AGAIN AND AGAIN →&nbsp;&nbsp;&nbsp;</big>'
      'onclick="togglePopup()"'content.Add _.Button,New _.img,⊂⊂'src' '/Examples/Data/smiley.gif'
     
      Add _.script ScriptFollows
      ⍝ function togglePopup() {
      ⍝     if ($("#popup").ejDialog("isOpen")) {
      ⍝         $("#popup").ejDialog("close");
      ⍝     } else {
      ⍝         $("#popup").ejDialog("open");
      ⍝     };
      ⍝ };
     
      dlg←Add _.ejDialog'#dialog' 'ejDialog Advanced example' 1
      'showOnInit' 'position.Y' 'position.X'dlg.Set _true 20 40
      dlg.(Resize Buttons)←0,⊂'close' 'maximise' 'minimise' 'collapsible' 'pin'
     
      'popup'Add _.div,New _.p'Ouch!!!'
      'showOnInit'(Add _.ejDialog'#popup' 'Smiley says:').Set _false
     
    ∇

:EndClass