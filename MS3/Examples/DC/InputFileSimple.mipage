:Class InputFileSimple : MiPageSample
⍝ Control:: _DC.InputFile _.DC.Input
⍝ Description:: Input-control to handle file-uploads

    ∇ Compose;out;fname;fdata;frm
      :Access Public
     
      Add MarkdownFollows
      ⍝ The Dyalog `InputFile` control is deprecated. Use `Input'file'` instread, like in this example. 
      ⍝ If a file is uploaded, the data linked to the name of the input control is a 2 element vector of vectors.
      ⍝ The first vector is the file name, the second vector is an integer vector (datatype 83) of the uploaded file's data
     
      out←'output'Add _.div
      :If _Request.isPost  ⍝ anything uploaded
      :AndIf ×≢Get'ipfl'
     
      ⍝ At this point _PageData.ipfl is a 2-element vector containing
      ⍝   [1] the file name
      ⍝   [2] the raw data for the file
      ⍝ However, rather than save the file to disk (and clutter up the MS3 MiSite)
      ⍝ we'll just calculate a simple checksum and display that.
     
          (fname fdata)←Get'ipfl'
          out.Add _.strong fname,' received with ',(⍕⍴fdata),' bytes and a checksum of ',⍕(255|+)/fdata
          out.Add¨2⍴_.br
      :EndIf
     
      frm←Add _.Form  ⍝ dialog is always shown so that we can upload again after having sent a file!
     
      'ipfl'frm.Add _.Input'file' 'Select a file to upload: '
     
      frm.Add _.br
      'submit'frm.Add _.Input'submit' 'Submit'
     
      ⍝↓↓↓ this is only so that the page will render properly in the MS3 MiSite's index page
      ⍝    after the form is submitted.  It normally would not be needed in most applications.
      ⍝'action'frm.Set _Request.Page,'?NoWrapper=1'
     
    ∇

:EndClass
