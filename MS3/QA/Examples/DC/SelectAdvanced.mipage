 msg←Test dummy;result;output;sel;froot;prev
⍝ Test /Examples/DC/SelectAdvanced

⍝ Ensure 'multi' (the selection list) is there:
 msg←'selection list not there'
 :If 0≢sel←Selenium.Find'multi'
   ⍝ Grab the 2 elements already chosen:
     Selenium.Click'PressMe'
     output←Selenium.Find'output'
     {0≠⍴output.Text}Selenium.Retry ⍬ ⍝ Wait to see if it gets populated
     msg←'Expected output was not produced.'
 :AndIf 'You picked: Bananas Pears'≡prev←output.Text
   ⍝ Make a single selection:
     froot←'Grapes'
     'multi'Selenium.SelectItemText'~'froot
     Selenium.Click'PressMe'
     output←Selenium.Find'output'
     {prev≢output.Text}Selenium.Retry ⍬ ⍝ Wait to see if it gets populated
 :AndIf (prev←output.Text)≡'You picked: ',froot
   ⍝ Make another selection:
     'multi'Selenium.SelectItemText'Pears'
     Selenium.Click'PressMe'
     output←Selenium.Find'output'
     {prev≢output.Text}Selenium.Retry ⍬ ⍝ Wait to see if it gets populated
 :AndIf (prev←output.Text)≡'You picked: ',froot,' Pears'
     msg←''
 :EndIf