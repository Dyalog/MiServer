:Class indexnew : MiPageSample  ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝
⍝ ┌─────────────────────────────────────────────────────────────────────────────────────────────┐ ⍝
⍝ │ This is the home page of the MiServer Sample Site, MS3, accessible at miserver.dyalog.com   │ ⍝
⍝ └─────────────────────────────────────────────────────────────────────────────────────────────┘ ⍝
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :Include #.MS3SiteUtils

    ∇ Compose;data;eg;main;ctrls;cats;list;titles;sections;cols
      :Access Public
     
      (titles sections)←⊂⍬
     
      titles,←⊂'Getting started with MiServer 3.0' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      sections,←'about'New _.div(WrapFollowing'p')
      ⍝ MiServer is a free, open-source web server implemented in Dyalog APL.
      ⍝ It enables the APL user to build sophisticated websites with little to no
      ⍝ knowledge of the underlying web technologies (HTML, JavaScript, CSS, etc).
      ⍝
      ⍝ This website serves as guide to MiServer 3.0 – providing documentation, samples,
      ⍝ and advice. At the same time, it showcases many of MiServer's capabilities,
      ⍝ as it is built almost entirely in APL using components available in MiServer.
      ⍝
      ⍝ Expand the below tabs to explore documentation, the broad selection of available controls,
      ⍝ and the vast collection of sample pages and mini-apps. See the source of any page by
      ⍝ clicking the large Dyalog <img id="Dyalog-D" src="/Examples/Data/Dyalog-D.png"
      ⍝ alt="Dyalog-D" height="20px" style="vertical-align:bottom;"> in the top left corner.
      ⍝
      ⍝ We hope that you will enjoy MiServer; join us on the forums (see the
      ⍝ <strong>Resources</strong> drop-down at the top right), and contribute samples or
      ⍝ extensions via <a id="GitHub" href="https://github.com/dyalog/miserver/">GitHub</a>.
     
      titles,←⊂'Documentation, techniques, and mini apps' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      list←⍬
      list,←'pdf'FormatList'Documentation'
      list,←FormatList'Examples/Techniques'
      list,←FormatList'Examples/Applications'
      sections,←'doc'New _.DataTable('Category' 'Subject'⍪↑list)
     
      titles,←⊂'Controls, widgets, and elements' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Documentation'),DocLink¨C.info[;1]
      cols,←⊂(⊂'Relevant samples'),RelDocs¨C.info[;1]
      cols,←⊂(⊂'Description'),C.info[;2]
      sections,←'ctrl'New _.DataTable(⍉↑cols)
     
      titles,←⊂'Small samples pages focusing on one or a few controls' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Main controls used'),{2↓∊', '∘,¨⍵}¨C.demoes
      cols,←⊂(⊂'Level'),'Advanced' 'Simple'[1+(∨/'Simple'∘⍷)¨C.files]
      cols,←⊂(⊂'Task demonstrated'),{Link('Description'Section Read ⍵)⍵}¨C.files
      sections,←'eg'New _.DataTable(⍉↑cols)
     
     ⍝⍝⍝ THE ACTUAL ACCORDION ⍝⍝⍝
      main←'content'Add _.jqAccordion(titles sections)
      'heightStyle'main.Set'content'
    ∇

      FormatList←{ ⍝ List of category-title pairs
          ⍺←1↓FILEEXT
          (⍺≡1↓FILEEXT)CatAndItem¨{'/'≠⊃⌽⍵}Of ⍺ List ⍵
      }

      CatAndItem←{ ⍝ (category) (filename/description)
          cat←2 LastSeg ⍵
          cat←'General' 'Mini App'cat['Documentation' 'Applications'⍳⊂cat]
          cat(Link((NoExt LastSeg)⍣(~⍺)('Description'Section Read)⍣⍺⊢⍵)⍵)
      }

      LastSeg←{ ⍝ ⍺th (default 1st) /-separated segment from the end
          ⍺←⊢
          1↓⍺⊃⌽('/'∘=⊂⊢)'/',⍵
      }

      Link←{ ⍝ New-tab link with optional (⍺) "tooltip"
          ⍺←0
          (('title=',Q ⍺){⍺ ⍵}⍣(⍺≢0)⊢'target="_blank"')New _.A ⍵
      }

      DocLink←{ ⍝ Link to WidgetDoc with appropriate parameters
          6::New¨(_.del ⍵)(_.small' deprecated')
          ref←⍎'_.',⍵
          ns←#.MS3SiteUtils.NSS(⊃⊣(/⍨)(∨/⍷)¨)⊂⍕ref
          link←'/Documentation/DyalogAPIs/WidgetDocNew?namespace=_',ns,'&widget=',⍵
          tip←{⍵↑⍨¯1+⌊/⍵⍳⎕UCS 13 10}'Constructor'Section ⎕SRC ref
          tip,←(''≡tip)/'[content]'
          tip Link ⍵ link
      }

      LinkWithTip←{ ⍝ Link with "tooltip" that has description and level
          tip←('Description'Section Read ⍵),' (Advanced)' ' (Simple)'⊃⍨1+∨/'Simple'⍷⍵
          ⍺≡0:Link tip ⍵
          tip Link ⍺ ⍵
      }

      Circle←{ ⍝ Circle a number using Unicode
          '&#',(9311+⍵),';'
      }

      RelDocs←{ ⍝ Links to related samples
          list←Relevant ⍵
          0=≢list:'(none)'
          nums←Circle¨⍳≢list
          nums LinkWithTip¨list
      }

:EndClass
