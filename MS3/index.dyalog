:Class index : MiPageSample

    :SECTION GLOBALS ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

    :Field Public Shared APPS          ⍝ List of all apps
    :Field Public Shared SAMPLES←0 3⍴⍬ ⍝ List of all samples
    :Field Public Shared GROUPS        ⍝ Names of groups of elements
    :Field Public Shared REFS          ⍝ ... their refs
    :Field Public Shared SEARCH←''     ⍝ Last search

    :ENDSECTION

    :SECTION UTILITIES ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

    Q←'"'∘,,∘'"'

    Words←{(1↓¨(' '∘=⊂⊢)' ',⍵)~⊂''}

    SpaceToDir←{'Examples/',1↓⍵,(⍵≡'_HTML')/'plus'} ⍝ _HTML is in the HTMLplus folder

    Dread←{0::,⊂'[file read failed]' ⋄ #.UnicodeFile.ReadNestedText #.Boot.AppRoot,⍵,'.dyalog'}

    Dlist←{0::⍬ ⋄ ¯7↓¨6⊃#.Files.DirX #.Boot.AppRoot,⍵,'/*.dyalog'}

    In←{∨/¨⊃⍷¨/{⎕SE.Dyalog.Utils.lcase¨eis ⍵}¨⍺ ⍵}

    ∇ node←Node
      node←⊃2⊃⎕VFI{((+\⍵='_')⍳2)↓⍵}⊃_PageData.node
    ∇

      Section←{ ⍝ extract section ⍺:: from code ⍵
          regex←'^\s*⍝\s*',⍺,':(:.*?)$((\R^⍝(?!\s*\w+::).*?$)*)'
          opts←('Mode' 'M')('DotAll' 1)
          res←(regex ⎕S'\1\2'⍠opts)⍵
          (1↓⊃res)~'⍝'
      }

    ∇ text←JSsafe text;fr;to
     ⍝ Make text JS safe by escaping problematic chars
     
     ⍝ ' " \ NL CR TB BS FF
      fr←'''' '"' '\\' '\n' '\r',⎕UCS 9 8 12
     ⍝ Problematic chars are often problematic in regex too :-)
      to←'\\''' '\\"' '\\\\' '\\n' '\\r' '\\t' '\\b' '\\f'
     ⍝ Replace problematic chars and sourround with quotes
      text←1⌽'""',(fr ⎕R to⍠'Mode' 'D')text
    ∇

    ∇ js←APLtoJS vtv;act;data
      :Access public shared
     ⍝ Convert vectors of pseudo JS (generated by Replace, Execute, et al.) to proper JS
     
      js←''
      :For act data :In eis vtv
          data←JSsafe⊃⌽eis data ⍝ the last or only one
          :Select ⊃eis act      ⍝ the first or only one
          :Case 'assign'
              js,←(⊃⌽act),' = ',data,';' ⍝ variable = "data"
          :Case 'execute'
              js,←'eval(',data,');' ⍝ eval("data")
          :Case 'replace'
              js,←'$("',(⊃⌽act),'").html(',data,');' ⍝ $("#myid").html("data")
          :Else
              js,←'$("',(⊃⌽act),'").',(⊃act),'(',data,');' ⍝ $("#myid").append("data")
          :EndSelect
      :EndFor
    ∇

    :ENDSECTION

    :SECTION MAIN ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

    ∇ Compose;left;mid;right;sp;vp
      :Access public
     
     ⍝ Initialize globals
      GROUPS←'Base HTML' 'Wrapped HTML' 'Dyalog Controls' 'JQueryUI' 'SyncFusion'
      REFS←_html _HTML _DC _JQ _SF
     
      #.IS←infoShort←'⍝',¨Dread'Examples/Data/infoShort'
      #.IL←infoLong←'⍝',¨Dread'Examples/Data/infoLong'
     
      Use'ejTab' ⍝ May get added by callbacks
     
      newdiv←{⍵ New _.div}                                  ⍝ Make a div
      textspan←{'.textspan'New _.span ⍵}                    ⍝ And a span
      horz←{⍺←⊣ ⋄ r⊣(r←⍺ New _.StackPanel ⍵).Horizontal←1}  ⍝ Horizontal StackPanel
     
      '#title'Add _.title'MS3: About' ⍝ After the : will be updated
     
      Add _.StyleSheet'/Styles/homepage.css'
     
      (left mid)←newdiv¨'leftBar' 'midBar' ⍝ Create panes
     
      sp←'mainSP'horz left mid
      sp.Items[1⊣3].style←⊂'width: 200px; max-height: 450px;'
      sp.Items[2].style←⊂'margin: 5px;'
      sp.style←'height: 450px; width: 100%;'
     
      vp←Add _.StackPanel sp
      vp.style←'width:100%'
     
      PopulateLeft left
      PopulateMid mid
    ∇

    ∇ PopulateLeft thediv;class;depths;group;items;names;ref;samples;tv;vp;search;case;style
     
      ⍝ SEARCH FIELD ⍝⍝⍝
      (search←New _.EditField'str').On'change' 'OnSearch'('str' 'val')
      ⍝style←'style="padding: 3px 1px 5px; font-family: APL385 Unicode; font-size: 0.8em;"'
      (case←'#case'New _.button'Search').On'click' 'OnSearch'
     
      (thediv.Add _.StackPanel(search case)).Horizontal←1
     
      thediv.Add _.hr
     
      ⍝ SAMPLE APPS ⍝⍝⍝
      APPS←(Dlist'Examples/Apps')~⊂'index'
      thediv.Add textspan'Sample Apps'
      tv←thediv.Add _.ejTreeView(1,[1.5]APPS)
      tv.style←'max-height: 250px'
      tv.On'nodeSelect' 'OnApp'('node' 'eval' 'argument.id')
     
      thediv.Add _.hr
     
      ⍝ CONTROLS ⍝⍝⍝
      :For (group ref) :InEach GROUPS REFS
          names←{({#.HtmlElement=⊃⊃⌽⎕CLASS ⍵}¨⍵)/⍵}ref.(⍎¨⎕NL ¯9.4)
          class←2↓⍕ref ⍝ Remove leading #.
          names←(3+⍴class)↓¨⍕¨names ⍝
          samples←class FindSamples names
          names←(⊂group),⊃,/(⊂¨names),¨samples
          depths←1,∊2,⍪(⍴¨samples)⍴¨3
          SAMPLES⍪←depths,(⍪names),(⊂class)
      :EndFor
      ⍝items←SAMPLES[;1],[1.5]({1↓¨(¯1+⍵⍳¨':')↑¨⍵}infoShort){⍵,¨(⍵∊⍺)/¨' ('∘,¨((4+⍴¨⍵)↓¨(infoShort,⊂'')[⍺⍳⍵]),¨')'}SAMPLES[;2] ⍝ add short info
      thediv.Add textspan'Controls'
      tv←thediv.Add _.ejTreeView(0 ¯1↓#.SAM←SAMPLES) ⍝ items
      tv.style←'max-height: 300px'
      tv.On'nodeSelect' 'OnControl'('node' 'eval' 'argument.id')
    ∇

    ∇ r←space FindSamples names;i;samples;suffix
      samples←Dlist SpaceToDir space
      r←(⍴names)⍴⊂''
     
      :For suffix :In 'Simple' 'Advanced'
          i←(((-⍴suffix)↑¨samples)∊⊂suffix)/⍳⍴samples
          i←{(⍵≤⍴names)/⍵}names⍳(-⍴suffix)↓¨samples[i]
          r[i]←r[i],¨⊂⊂suffix
      :EndFor
    ∇

    ∇ PopulateMid mid;url;code;frame;mya
     
     ⍝ Read framed pages
      url←'Examples/Apps/About'
      code←Dread url
     
     ⍝ Create and fill placeholder for title header
      mya←('#SampleTitle'mid.Add _.h2).Add _.a('Control'Section code)
      'target' 'href'mya.Set'_blank'url
     
     ⍝ Create and fill placeholder for description line
      '#SampleDesc'mid.Add _.p('Description'Section code)
     
     ⍝ Create and fill placeholder for embedded page
      frame←'#SampleFrame' 'style="background-color:white"'mid.Add _.div
      ('src=',url,'?NoWrapper=1')'width="800"' 'height="400"'frame.Add _.iframe
      '#SampleSource'mid.Add _.div(#.HTMLInput.APLToHTMLColour code)
    ∇

    :ENDSECTION

    :SECTION CALLBACKS ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

    ∇ r←OnApp
      :Access Public
     ⍝ Gets called upon selection in Sample Apps tree
      r←GenJS Update'Examples/Apps'(Node⊃APPS)
    ∇

    ∇ r←OnControl;node;depth;sample;control;space;spacedir;files;out;file;code;ctrlsec;url;iframe;page;desc;title;item;spacectrl
      :Access Public
     ⍝ Gets called upon selection in Controls tree
     
     ⍝ Get details
      node←Node
      depth←⊃SAMPLES[node;1]
      control←depth⊃''(⊃SAMPLES[node;2])(⊃SAMPLES[1⌈1+node-2⍳⍨⌽node↑SAMPLES[;1];2])
      sample←depth⊃'index' ''(control,⊃SAMPLES[node;2])
      space←⊃SAMPLES[node;3]
      spacedir←SpaceToDir space
     
      :If depth=2 ⍝ Element
          spacectrl←space,'.',control
          files←⊃↓⍉Dlist spacedir
          out←'style="width:800px;height:400px;border:2px inset"'New _.div
     
          :For file :In files
              url←spacedir,'/',file
              code←Dread url
              ctrlsec←'Control'Section code
              :If (⊂spacectrl)∊Words ctrlsec ⍝ Extract space-separated words
                  iframe←'src' 'width' 'height'(New _.iframe).Set(url,'?NoWrapper=1')800 400
                  page←¯7↓file
                  desc←'Description'Section code
                  title←New _.a ctrlsec               ⍝ link with element names
                  'target' 'href'title.Set'_blank'url ⍝ new tab
                  item←'style="margin:8px"'out.Add _.p desc
                  item.On'click' 0 ''(APLtoJS GenJS page title desc iframe code)
              :EndIf
          :EndFor
          title←1↓control Section infoShort
          title←spacectrl,(×≢title)/' (',title,')'
          desc←control Section infoLong
          r←spacectrl title desc out''
     
      :Else ⍝ Sample or Group
          r←Update spacedir sample
      :EndIf
      r←GenJS r
    ∇

    ∇ (page title desc iframe code)←Update(spacedir page);ctrlsec;url
     ⍝ Create new placeholder values
      url←spacedir,'/',page
      iframe←'src' 'width' 'height'(New _.iframe).Set(url,'?NoWrapper=1')800 400
      code←Dread url
      ctrlsec←'Control'Section code
      desc←'Description'Section code
      title←'target' 'href'(New _.a ctrlsec).Set'_blank'url
     
    ∇

    ∇ r←GenJS(page title desc iframe code)
     ⍝ Generate JavaScript for filling placeholders
      r←'#title'Replace'MS3: ',page
      r,←'#SampleTitle'Replace title
      r,←'#SampleDesc'Replace desc
      r,←'#SampleFrame'Replace iframe
      r,←'#SampleSource'Replace(×≢code)/#.HTMLInput.APLToHTMLColour code
    ∇

    ∇ r←OnSearch;dir;files;file;code;desc;str;terms;entry;url;ctrlsec;iframe;page;title;item;out;i;time
      :Access Public
      time←0.001×3⊃⎕AI
      str←Get'str'              ⍝ get search string
      SEARCH←str,SEARCH/⍨~×≢str ⍝ use last search if empty
      str←⊂SEARCH
     ⍝⍝⍝ Controls
     
      terms←infoShort/⍨str In infoShort
      terms,←terms~⍨infoLong/⍨str In infoLong
      terms←'.',¨1↓¨∪(¯1+terms⍳¨':')↑¨terms
      #.RES←''
     ⍝⍝⍝ Samples
      out←'style="width:800px;height:400px;border:2px inset"'New _.div
      i←0
      :For dir :In 'Apps' 'html' 'HTMLplus' 'DC' 'JQ' 'SF'
          files←Dlist'Examples/',dir
          ⍝types←'Simple' 'Advanced'
          :For file :In files~⊂'index'
              url←'Examples/',dir,'/',file
              code←Dread url
              desc←⊂'Description'Section code
              ctrlsec←'Control'Section code
              :If ∨/str In desc
              :OrIf ∨/∊(str,terms)⍷¨⊂ctrlsec ⍝ always case sensitive
                  i+←1
                  :If 16≥i
                      iframe←'src' 'width' 'height'(New _.iframe).Set(url,'?NoWrapper=1')800 400
                      page←¯7↓file
                      title←New _.a ctrlsec               ⍝ link with element names
                      'target' 'href'title.Set'_blank'url ⍝ new tab
                      item←'style="margin:8px"'out.Add _.p,desc
                      item.On'click' 0 ''(APLtoJS GenJS page title,desc,iframe code)
                  :EndIf
              :EndIf
          :EndFor
      :EndFor
      page←Q str
      title←'Search: ',page,' (consider narrowing your search)'/⍨16<i
      time-⍨←0.001×3⊃⎕AI
      i←(⍕16⌊i),(16<i)/' of ',(⍕i)
      desc←(⍕i),' results (',time,' seconds)',(×≢terms)/' – included',(1↓∊', '∘,¨Q¨1↓¨terms),' in search'
      r←GenJS page title desc out''
     
    ∇

    :ENDSECTION

:EndClass