:Class index : MiPageSample ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝
⍝ ┌─────────────────────────────────────────────────────────────────────────────────────────────┐ ⍝
⍝ │ This is the home page of the MiServer Sample Site, MS3, accessible at miserver.dyalog.com   │ ⍝
⍝ └─────────────────────────────────────────────────────────────────────────────────────────────┘ ⍝
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :Include #.MS3SiteUtils

    ∇ Compose;data;eg;main;ctrls;cats;list;titles;sections;cols;gh;text;tree;reading
      :Access Public
     
      (titles sections)←⊂⍬
     
      titles,←⊂'Getting started' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      gh←(External'https://github.com/dyalog/miserver/').Render
      text←'about'New _.div('%gh%'gh Subst WrapFollowing'p')
      ⍝ MiServer is a free, open-source web server implemented in Dyalog APL.
      ⍝ It enables the APL user to build sophisticated websites with little to no
      ⍝ knowledge of the underlying web technologies (HTML, JavaScript, CSS, etc).
      ⍝
      ⍝ This website serves as guide to MiServer 3.0 – providing documentation, samples,
      ⍝ and advice. At the same time, it showcases many of MiServer's capabilities. If you are just
      ⍝ getting started with MiServer 3.0, we recommend going through the foundational
      ⍝ documentation in the reading order of the tree to the right.
      ⍝
      ⍝ Explore the tabs for documentation and mini-apps, the broad selection of available controls,
      ⍝ and the vast collection of sample pages. See the source of any page by
      ⍝ clicking the large Dyalog <img id="Dyalog-D" src="/Examples/Data/Dyalog-D.png"
      ⍝ alt="Dyalog-D"> in the top left corner.
      ⍝
      ⍝ We hope that you will enjoy MiServer; join us on the forums (see the <strong>Resources
      ⍝ </strong> drop-down at the top right), and contribute samples or extensions via
      ⍝ GitHub%gh%.
      reading←'/',¨(Read'Examples/Data/reading.txt')~⊂''
      reading←{(Name∘NoExt¨⍵)(Levels ⍵)(DocTreeLink¨⍵)}reading
      tree←'#reading'New _.List reading
      sections,←New _.Table(1 2⍴text tree)
     
      titles,←⊂'Documentation & mini apps' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      list←⍬
      list,←'pdf'FormatList'Documentation'
      list,←FormatList'Examples/Techniques'
      list,←FormatList'Examples/Applications'
      sections,←'doc'New _.DataTable('Category' 'Subject'⍪↑list)
     
      titles,←⊂'Controls & widgets' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Control<br><small>(documentation link)<small>'),DocLink¨C.info[;1]
      cols,←⊂(⊂'Relevant samples'),RelDocs¨C.info[;1]
      cols,←⊂(⊂'Description'),C.info[;2]
      sections,←'ctrl'New _.DataTable(⍉↑cols)
     
      titles,←⊂'Sample pages' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Task demonstrated'),{Link('Description'Section Read ⍵)⍵}¨C.files
      cols,←⊂(⊂'Level'),'Advanced' 'Simple'[1+(∨/'Simple'∘⍷)¨C.files]
      cols,←⊂(⊂'Main controls used'),{2↓∊(', ',DocLink)¨⍵}¨C.demoes
      sections,←'eg'New _.DataTable(⍉↑cols)
     
     ⍝⍝⍝ THE ACTUAL TABS CONTROL ⍝⍝⍝
      Add BigTabs titles sections
    ∇

:EndClass
