:Class index : MiPageSample ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝
⍝ ┌─────────────────────────────────────────────────────────────────────────────────────────────┐ ⍝
⍝ │ This is the home page of the MiServer Sample Site, MS3, accessible at miserver.dyalog.com   │ ⍝
⍝ └─────────────────────────────────────────────────────────────────────────────────────────────┘ ⍝
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :Include #.MS3SiteUtils

    ∇ Compose;data;eg;main;ctrls;cats;list;titles;sections;cols;text;tree;reading;forums;github;substs
      :Access Public
     
      (titles sections)←⊂⍬
     
      titles,←⊂'Getting started' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      forums←('fo&shy;rums'External'http://www.dyalog.com/forum/viewforum.php?f=34').Render
      github←('Git&shy;Hub'External'https://github.com/dyalog/miserver/').Render
      substs←('%forums%'forums)('%GitHub%'github)('~' '&shy;')
      (text←'about'New _.table).Add _.tr (substs Subst WrapFollowing'td')
      ⍝ Mi~Serv~er is a free, o~pen-source web serv~er im~ple~ment~ed in Dyalog
      ⍝ APL. It en~a~bles the APL us~er to build so~phis~ti~cat~ed websites
      ⍝ with lit~tle to no knowl~edge of the un~der~ly~ing web
      ⍝ tech~nol~o~gies (HTML, Java~Script, etc). We hope that you will en~joy
      ⍝ Mi~Serv~er; join us on the %forums%, and con~tri~bute sam~ples or
      ⍝ ex~ten~sions vi~a %GitHub%.
      ⍝
      ⍝ This web~site serves as guide to Mi~Serv~er 3.0 – pro~vid~ing
      ⍝ doc~u~men~ta~tion, sam~ples, and ad~vice. At the same time, it show~cas~es
      ⍝ man~y of Mi~Serv~er's ca~pa~bil~i~ties. If you are just get~ting start~ed with
      ⍝ Mi~Serv~er 3.0, we rec~om~mend go~ing through the foun~da~tion~al
      ⍝ doc~u~men~ta~tion in the read~ing or~der of right-hand tree.
      ⍝
      ⍝ Ex~plore the tabs for doc~u~men~ta~tion, the broad se~lec~tion of
      ⍝ a~vail~a~ble con~trols, and the vast col~lec~tion of sam~ple
      ⍝ pag~es and mi~ni~apps. See the source of an~y page by click~ing the
      ⍝ MS3 lo~go in the top left cor~ner and ac~cess oth~er re~sourc~es
      ⍝ by hov~er~ing o~ver the top right Dyalog lo~go.
     
      reading←'/',¨(Read'Examples/Data/reading.txt')~⊂''
      reading←{(Name∘NoExt¨⍵)(Levels ⍵)(DocTreeLink¨⍵)}reading
      tree←'#reading'New _.List reading
      sections,←New _.Table(1 2⍴text tree)
     
      titles,←⊂'Documentation & mini apps' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      list←⍬
      list,←'pdf'FormatList'Documentation'
      list,←FormatList'Examples/Techniques'
      list,←FormatList'Examples/Applications'
      sections,←'doc'New _.DataTable('Category' 'Subject'⍪↑list)
     
      titles,←⊂'Controls & widgets' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Control<br><small>(documentation link)<small>'),DocLink¨C.info[;1]
      cols,←⊂(⊂'Relevant samples'),RelDocs¨C.info[;1]
      cols,←⊂(⊂'Description'),C.info[;2]
      sections,←'ctrl'New _.DataTable(⍉↑cols)
     
      titles,←⊂'Sample pages' ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      cols←⍬
      cols,←⊂(⊂'Task demonstrated'),{Link('Description'Section Read ⍵)⍵}¨C.files
      cols,←⊂(⊂'Level'),'Advanced' 'Simple'[1+(∨/'Simple'∘⍷)¨C.files]
      cols,←⊂(⊂'Main controls used'),{2↓∊(', ',DocLink)¨⍵}¨C.demoes
      sections,←'eg'New _.DataTable(⍉↑cols)
     
     ⍝⍝⍝ THE ACTUAL TABS CONTROL ⍝⍝⍝
      Add BigTabs titles sections
    ∇

:EndClass
