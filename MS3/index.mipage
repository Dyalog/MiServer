:Class index : MiPageSample ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝
⍝ ┌─────────────────────────────────────────────────────────────────────────────────────────────┐ ⍝
⍝ │ This is the home page of the MiServer Sample Site, MS3, accessible at miserver.dyalog.com   │ ⍝
⍝ ├─────────────────────────────────────────────────────────────────────────────────────────────┤ ⍝
⍝ │ The code is split up in three sections called MAIN, CALLBACKS, and SHOW:                    │ ⍝
⍝ │                                                                                             │ ⍝
⍝ │     ∘ MAIN is doing all the initial set-up , building the various parts of the page, set-   │ ⍝
⍝ │       ting the inital values of globals, and binding event-handlers to clicks, etc.         │ ⍝
⍝ │                                                                                             │ ⍝
⍝ │     ∘ CALLBACKS resumes control after the user triggers one of the handlers and redirects   │ ⍝
⍝ │       to appropriate feedback.                                                              │ ⍝
⍝ │                                                                                             │ ⍝
⍝ │     ∘ SHOW generates the necessary code to make the browser displaying appropriate data     │ ⍝
⍝ │       in their respective interface areas.                                                  │ ⍝
⍝ │                                                                                             │ ⍝
⍝ │ There is also a dependancy, the scripted namespace indexUtils, which contains all general   │ ⍝
⍝ │ and special sub-routines used by function in the above sections.                            │ ⍝
⍝ └─────────────────────────────────────────────────────────────────────────────────────────────┘ ⍝
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :SECTION A_MAIN ⍝ ACTIONS BEFORE FIRST USER INTERACTION

    :Include indexUtils ⍝∇:require =/indexUtils

   ⍝ These are all the global variable being used:
    :Field shared GROUPS←'Dyalog' 'SyncFusion' 'JQueryUI' 'Base HTML' ⍝ groups of elements
    :Field shared NSS←   'DC'     'SF'         'JQ'       'html'      ⍝ corresponding nss
    :Field shared FILEEXT←'.mipage'                                   ⍝ Server.xml:DefaultExtension
    :Field shared CACHE←'#.CACHE'                                     ⍝ location of cache
    :Field shared TREE                                                ⍝ Controls tree
    :Field CURRFILES←,⊂'/About_MiServer'                              ⍝ currently relevant files

    ∇ Cache ⍝ Set up cache only once
      :Access public
      :Implements constructor
      TREE←(Read'Examples/Data/tree.txt')~⊂'' ⍝ load/cache tree and remove blank lines
    ∇

    ∇ Compose ⍝ This builds the page
      :Access public
      Add _.StyleSheet'/Styles/homepage.css'   ⍝ assign cascading style sheet
      Add'mainSP'Horz MakeLeft MakeMid         ⍝ two panels
    ∇

    ∇ left←MakeLeft;sf;sb;tree;f ⍝ Make the left panel with search and two tree-views
      left←'#leftBar'New _.div
     
      sf←'autofocus='New _.EditField'searchfield'         ⍝ search field active on page load
      sb←'#searchbutton' '.menu'New _.button'Search'      ⍝ search button (note: type="submit")
      f←'onsubmit="return false;"'left.Add _.form         ⍝ prevent submit-button from POSTing
      f.On'submit' 'OnSearch'('str' '#searchfield' 'val') ⍝ callback upon Enter or button-click
      f.Add Horz sf sb                                    ⍝ textfield and button next to each other
     
      left.Add¨Tree¨'Getting Started' 'Controls' ⍝ NB: Needs MakeTreeXX (XX is initials of title)!
    ∇

    ∇ tree←MakeTreeGS ⍝ Make the "Getting Started" tree
      :Access public shared 
      tree←⍉⍪1 'Start here',⊂∊CURRFILES FILEEXT ⍝ top level
      tree⍪←'pdf'DirTree'Documentation'         ⍝ documentation is PDFs only
      tree⍪←DirTree'Examples/Techniques'
      tree⍪←DirTree'Examples/Applications'
      tree[;2]←Clean¨tree[;2]  ⍝ sample apps have underscores for spaces
    ∇

    ∇ tree←MakeTreeC;nss ⍝ Make the "Controls" tree
      :Access public shared
      tree←1 'Core' '/*'BuildTree Core TREE
      tree,¨←1 'All' '/'BuildTree NoSt TREE
      nss←(NSS∊⍨2⊃tree)/⍳⍴⊃tree               ⍝ which ones are roots
      (2⊃tree)[nss]←GROUPS[NSS⍳(2⊃tree)[nss]] ⍝ replace them with proper names
      tree←⍉↑tree
    ∇

    ∇ mid←MakeMid;url;code;title;attrs;d;hidden ⍝ Make middle pane; info, framed content, source
      mid←'#midBar'New _.div         ⍝ container
      hidden←'style="display:none;"' ⍝ attribute to start hidden
      code←Read url←⊃CURRFILES       ⍝ read initial page
     
      title←Clean'Control'Section code   ⍝ create and fill placeholder for title
      '#Title'Add _.title('MS3: ',title) ⍝ this goes on the browser tab
     
      hidden'#Header'mid.Add _.h2        ⍝ placeholder for category lists and search results
      d←'#Sample'mid.Add _.div           ⍝ container for file info so visibility can be toggled
      '#SampleTitle'd.Add _.strong title ⍝ filename
      d.Add'– '
      '#SampleDesc'd.Add _.span('Description'Section code) ⍝ sample description
      ('#PopLink'd.Add _.span).Add NewWinA'Pop out'url     ⍝ "Pop out" link
      d.Add _.br
     
      d←hidden'#Control'mid.Add _.div ⍝ container for widget info for toggleable visibilty
      '#ControlTitle'd.Add _.strong   ⍝ placeholder for widget name
      d.Add'– '
      '#ControlDesc'd.Add _.span ⍝ placeholder for widget description
      '#DocLink'd.Add _.span     ⍝ placeholder for link to WidgetDoc page
      d.Add _.br
     
      attrs←'#ShowRelated' 'onchange=$("#rel").toggle(400)'              ⍝ built-in toggleing
      attrs d.Add _.Input'checkbox' '' 'Show 0 related samples ' 'right' ⍝ checkbox to show related
      hidden'#rel'd.Add _.div                                            ⍝ list starts hidden
     
      mid.Add _.hr
      ('#SampleFrame'mid.Add _.div).Add Frame url ⍝ embedded page
      mid.Add _.hr
     
      attrs←'#ShowSource' 'onchange=$("#SampleSource").toggle(400)'    ⍝ smooth transition
      attrs mid.Add _.Input'checkbox' '' 'Show source code' 'right'    ⍝ toggle show/hide source
      hidden'#SampleSource' '.samplesource'mid.Add _.div Ⓒ Format code ⍝ ... starts hidden
     
      '#Handler'mid.Add _.div ⍝ placeholder for list item event handler
    ∇

    :ENDSECTION ⍝ ─────────────────────────────────────────────────────────────────────────────────
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :SECTION B_CALLBACKS ⍝ PROGRAMS THAT RUN AFTER THE USER TRIGGERS AN EVENT HANDLER

    ∇ r←OnTree;node;list;control;nost;ns ⍝ Handle callbacks from the ejTreeViews
      :Access Public
      nost←'*'~⍨node←Get'node'                        ⍝ was sent along by the event handler
      :If Sl Z nost                                   ⍝ if selected a category:
          :If 'GS'In _what                                ⍝ if selected node of "Getting Started":
              r←('pdf'/⍨'Documentation'In nost)ShowFiles node ⍝ restrict to PDFs if documentation
          :Else                                           ⍝ if selected node under "Controls":
              r←ShowCat node                                  ⍝ show all files in that category
          :EndIf
      :ElseIf '.'∊nost                                ⍝ if filename with extention, e.g. other.pdf:
          r←ShowFile nost                                 ⍝ load it up
      :Else                                           ⍝ if selected a widget:
          CURRFILES←Relevant LastSub nost                 ⍝ find relevant samples
          :If ×≢CURRFILES                                 ⍝ if there is at least one:
              r←ShowSample nost                               ⍝ show it
          :Else                                           ⍝ if no samples:
              r←ShowNone nost                                 ⍝ display a message
          :EndIf
      :EndIf
    ∇

    ∇ r←OnList ⍝ Handle callbacks from lists of samples
      :Access Public
      r←ShowFile _what ⍝ just show it
    ∇

    ∇ r←OnSearch;str ⍝ Callback for search
      :Access Public
      str←Get'str'                                        ⍝ sent along by event handler
      :If str≡CaseLess'clear cache'                       ⍝ special phrase to reset the cache
          ⎕EX CACHE                                       ⍝ erase old cache
          r←Execute'alert("',CACHE,' has been cleared.")' ⍝ pop-up message
      :Else
          r←ShowResult str ⍝ perform the search
      :EndIf
    ∇

    :ENDSECTION ⍝ ─────────────────────────────────────────────────────────────────────────────────
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :SECTION C_SHOW ⍝ SUB-ROUTINES THAT GENERATE JAVASCRIPT TO SHOW NEW STUFF

    ∇ r←ShowResult str;terms;urls
      r←''
     
      terms←Words str                                    ⍝ make quote and VTV
      :If ''''∊str                                       ⍝ literal words if quotes:
          terms~¨←''''                                       ⍝ remove the quotes
          str←⍕Q¨terms                                       ⍝ rebuild search string
          terms,←C.info[;1]/⍨∨/∨⌿terms∘.{⍵∊⍨⊂⍺}Words¨C.info  ⍝ look for whole words
      :Else
          str←Q str
          terms,←C.info[;1]/⍨∨/∨⌿terms∘.In CaseLess C.info   ⍝ look for sub-strings
      :EndIf
      urls←'/',¨∪⊃,/(⊂''),Relevant¨∪terms                ⍝ find relevant files
     
      r,←'#Title'Replace'MS3: ',str                      ⍝ browser tab
      r,←'#Header'Replace(⍕≢urls),' samples for ',str    ⍝ "3 samples for Button" heading
      r,←'#SampleFrame'Replace∊ListItem¨SortFiles urls   ⍝ output the sorted  list
      r,←RefreshHandler                                  ⍝ we must refresh the handler
      r,←'#SampleSource'Replace''                        ⍝ clear source code area
      r,←Show'#Header'                                   ⍝ show search result title
      r,←Hide'#Control'                                  ⍝ no widget
      r,←Hide'#Sample'                                   ⍝ no page either
    ∇

    ∇ r←ShowFile node;name;url;code;nost ⍝ Display a MiPage or PDF in the middle and uppdate info
      r←''
      nost←node~'*'
      url←NoExt nost
      name←Clean LastSub url
      r,←'#Title'Replace'MS3: ',name                   ⍝ browser tab
      :If FILEEXT In nost                              ⍝ if an embedded page:
          CURRFILES(⊢,~)←⊂url                              ⍝ move file to front of list
          code←Read url                                    ⍝ read the source
          r,←'#SampleTitle'Replace name                    ⍝ filename
          r,←'#SampleDesc'Replace'Description'Section code ⍝ page description
          r,←'#PopLink'Replace NewWinA'Pop out'url         ⍝ link to open in new tab
          r,←'#SampleFrame'Replace Frame nost              ⍝ embedded page
          r,←'#SampleSource'Replace Format code            ⍝ syntax coloured source code
          r,←Hide'#Header'                                 ⍝ hide heading
          r,←Show'#Sample'                                 ⍝ display file name and description
          :If IsDemo nost                                  ⍝ if sample page:
              r,←ReplaceRelMsg                                 ⍝ replace page-count message
              r,←'#rel'Replace∊ListItem¨1↓CURRFILES,¨'*'       ⍝ add * to differ from tree nodes
              r,←RefreshHandler                                ⍝ refresh handler for new items
              r,←(Show⊣⍣('*'∊node)Hide)'#Control'              ⍝ hide info if called from list
          :Else                                            ⍝ if app or info page:
              r,←Execute'$("#Control").hide()'                 ⍝ no widget info
          :EndIf
      :Else                                                ⍝ if other document (PDF):
          r,←'#Header'Replace name                             ⍝ filename
          r,←'#SampleFrame'Replace NewObj nost                 ⍝ insert as object
          r,←'#SampleSource'Replace''                          ⍝ empty source code container
          r,←Show'#Header'                                     ⍝ show the filename
          r,←Hide'#Control'                                    ⍝ hide the widget info
          r,←Hide'#Sample'                                     ⍝ and the file info
      :EndIf
    ∇

    ∇ r←ShowNone node;name;ns;control;msg ⍝ Display generic message when there is no sample to show
      r←''
      name←∊'_.',¨(ns control)←NsControl node                         ⍝ what we wanted sample for
      r,←'#Title'Replace'MS3: ',name                                  ⍝ browser tab
      r,←'#SampleTitle'Replace name                                   ⍝ widget name
      r,←'#ControlDesc'Replace Info name                              ⍝ we may have some info on it
      r,←'#DocLink'Replace NewWinA'View documentation'Doc ns control  ⍝ and a link for more info
      r,←'#rel'Replace''                                              ⍝ there are no related pages
      r,←Hide'#Header'                                                ⍝ hide larger header
      r,←Show'#Control'                                               ⍝ show widget info
      r,←Hide'#Sample'                                                ⍝ hide nonexistant info
      msg←'<p>Sorry, no samples available for <em>',name,'</em></p>'  ⍝ message about problem...
      r,←'#SampleFrame'Replace msg                                    ⍝ ... goes instead of sample
      r,←'#SampleSource'Replace''                                     ⍝ no source either
    ∇

    ∇ r←ShowSample node;code;title;url;control;doc;info;ns;name ⍝ Load sample page and update info
      r←''
      name←∊'_.',¨(ns control)←NsControl node ⍝ full proper name
      info←Info name
      code←Read url←⊃CURRFILES ⍝ the first is what we display, the rest are related
      title←LastSub url
      r,←'#Title'Replace'MS3: ',title                                  ⍝ browser tab
      r,←'#SampleTitle'Replace title                                   ⍝ filename
      r,←'#SampleDesc'Replace'Description'Section code                 ⍝ sample description
      r,←'#PopLink'Replace NewWinA'Pop out'url                         ⍝ update new-tab link
      r,←'#SampleFrame'Replace Frame url                               ⍝ load sample page
      r,←'#SampleSource'Replace Format code                            ⍝ update source code
      r,←Hide'#Header'                                                 ⍝ not a list
      r,←Show'#Sample'                                                 ⍝ do show the sample's info
      :If IsDemo url                                                   ⍝ if sample page:
          r,←'#ControlTitle'Replace name                                   ⍝ filename
          r,←'#ControlDesc'Replace info                                    ⍝ widget info
          r,←'#DocLink'Replace NewWinA'View documentation'Ⓒ Doc ns control ⍝ update WidgetDoc link
          r,←ReplaceRelMsg                                                 ⍝ update count
          r,←'#rel'Replace∊ListItem¨1↓CURRFILES,¨'*'                       ⍝ update list
          r,←'#rel'Append New _.Handler'.listitem' 'click' 'OnList'        ⍝ refresh handler
          r,←Show'#Control'                                                ⍝ show it all
      :Else                                                            ⍝ if app or info page:
          r,←Hide'#Control'                                                ⍝ hide info and related
      :EndIf
    ∇

    ∇ r←{ext}ShowFiles node;list;name ⍝ Show list of files
      r←''
      :If 900⌶⍬ ⋄ :OrIf 0=⍴ext ⋄ ext←1↓FILEEXT ⋄ :EndIf ⍝ default file extention
      list←ext List node                                ⍝ get list of all those files
      list←~∘Sl∘Z Of list                               ⍝ remove folders
      r,←'#SampleFrame'Replace∊ListItem¨SortFiles list  ⍝ sorted list
      r,←RefreshHandler                                 ⍝ refresh handler because of new items
      r,←'#SampleSource'Replace''                       ⍝ no source code
      r,←'#Title'Replace'MS3: ',name←LastSub ¯1↓node    ⍝ browser tab
      r,←'#Header'Replace name                          ⍝ big heading
      r,←Show'#Header'                                  ⍝ show it too
      r,←Hide'#Control'                                 ⍝ no widget
      r,←Hide'#Sample'                                  ⍝ no file either
    ∇

    ∇ r←ShowCat node;tree;urls;name;core;cats ⍝ Show samples for all the widgets in a category
      r←''
      core←'*'∊node                                       ⍝ is it a core widget
      tree←NoSt'*'∘∊Of⍣core⊢TREE                          ⍝ filter for core-controls only
      node~←'*'                                           ⍝ ignore stars
      urls←∪⊃,/(⊂''),Relevant¨LastSub¨tree/⍨⊃¨node∘⍷¨tree ⍝ unique samples relevant to one of ours
      cats←1↓¨¯1↓node⊂⍨Sl node                            ⍝ find the top level ns
      (1↑cats)←(GROUPS,⊂'')[NSS⍳1↑cats]                   ⍝ replace by a proper name if abbr
      name←⍕('All' 'Core'⊃⍨1+core)(2↑cats)                ⍝ appropriate prefix
      r,←'#Title'Replace'MS3: ',name                      ⍝ browser tab
      r,←'#SampleFrame'Replace∊ListItem¨SortFiles urls    ⍝ sorted list
      r,←RefreshHandler                                   ⍝ refresh handler because of new items
      r,←'#SampleSource'Replace''                         ⍝ no source
      r,←'#Header'Replace name,' Controls'                ⍝ big heading
      r,←Show'#Header'                                    ⍝ show it too
      r,←Hide'#Control'                                   ⍝ no widget
      r,←Hide'#Sample'                                    ⍝ no file either
    ∇

    :ENDSECTION ⍝ ─────────────────────────────────────────────────────────────────────────────────
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

:EndClass