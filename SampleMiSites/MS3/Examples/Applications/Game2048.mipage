:class Game2048 :MiPageSample
⍝ Control:: _DC.Table
⍝ Description:: The addictive "2048" sliding game

    ∇ Compose;u;l;r;d;board;help
      :Access Public           ⍝ quick win ⍝ quick lose
      ⎕RL←⍬ ⋄ BOARD←Put2 4 4⍴0 ⍝⊢0 0 512   ⍝⊢ 0 8 4 16 32 64 128 256 512
      Add _.style ScriptFollows
      ⍝ #game {font-size:xx-large; margin: auto;}
      ⍝ #game td {vertical-align: middle; text-align: center;}
      ⍝ .slide, #help {background: darkorange; color:white; cursor: pointer; line-height: 2em;
      ⍝      border-radius: 2em; padding: 5px; margin: auto; height: 2em; width: 2em;}
      ⍝ #board td {height: 3em; width: 3em; max-width: 3em; overflow: visible;
      ⍝      background: #FFC680; box-shadow: 0 0 0 3px white inset;}
      ⍝ #status {position: fixed; text-align: center; top: 75px; bottom: 0; left: 0; right: 0;
      ⍝      color: darkorange; font-size: 2em; line-height: calc(100vh - 2em); cursor: pointer;}
      help←'#help' 'target="_blank"'New _.A'&nbsp;?&nbsp;' 'https://en.wikipedia.org/wiki/2048_(video_game)#Gameplay'
      (u l r d)←'a1' 'a0' 'a2' 'a3'{⍺'.slide'New _.div ⍵}¨'&#9650;' '&#9668;' '&#9658;' '&#9660;'
      'game'Add _.Table(3 3⍴''u help l('board'New _.div(Disp BOARD))r''d)
      Add _.Handler'.slide' 'click' 'OnSlide'
      Add _.script'$(document).keydown(function(x){return!$("#a"+(x.which-37)).click().length[0]})'
    ∇

    ∇ r←OnSlide;try;already
      :Access public
      r←''
      already←Won BOARD
      try←BOARD Slide⍎1↓_target
      :If try≢BOARD
          BOARD←Put2 try
          r,←'#board'Replace Disp BOARD
          :If already<Won BOARD
              r,←Status'WIN &ndash; click to clear'
          :ElseIf (Won BOARD)⍱1∊(0∘=,(⍉2=⌿⊢),2=/⊢)BOARD
              r,←Status'LOSS &ndash; click to clear'
          :EndIf
      :EndIf
    ∇

    Won←2048≤⌈/∘∊
    Put2←{t←⍵ ⋄ ((0=∊t)/∊t)[?+/0=∊t]←2+2×1=?10 ⋄ t}
    Disp←{w←⍵ ⋄ ((0=∊w)/∊w)←' ' ⋄ New _.Table w}
    Pack←{4↑⎕UCS(2/¨⎕UCS¨t)⎕R(,¨⎕UCS 2×t←2*⍳16)⎕UCS ⍵~0}
    Under←{⍵⍵⍣¯1 ⍺⍺ ⍵⍵ ⍵}
    Slide←{Pack¨Under↓Under(⍉∘⌽⍣⍵)⊢⍺}
    Status←{'#contentblock'Append'#status' 'onclick="window.location.reload()"'New _.div ⍵}
:endclass
