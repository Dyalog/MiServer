:Class Game_of_Life : MiPageSample
⍝ Control:: _DC.Table _DC.RadioButtonGroup _DC.Timer
⍝ Description:: John Conway's "Game of Life" (interactive)

    :Field public WORLD
    :Field public SHAPE
    :Field public STEP

    ∇ Setup
      :Access Public
      :Implements Constructor
      INIT←'Acorn'
      SHAPES←'Plane' 'Cylinder' 'Torus' 'Möbius' 'Klein'
      SHAPE←3⊃SHAPES
      LIVE←'.live slot'New _.div
      DEAD←'.dead slot'New _.div
      SURVIVOR←'.surv slot'New _.div
      STEP←1
      RUN←0
    ∇

    ∇ Compose
      :Access Public
     
      Add _.style ScriptFollows
      ⍝ .slot {height: 30px; width: 30px;}
      ⍝ .live, .dead, .surv {border-radius: 33%; position: absolute; z-index: -1;}
      ⍝ .live {background: LimeGreen; background: radial-gradient(DarkGreen 33%,aqua);}
      ⍝ .surv {background: DarkGreen; background: radial-gradient(DarkGreen 33%,khaki);}
      ⍝ .dead {background: Khaki; background: radial-gradient(Khaki,White);}
      ⍝ #world td {box-shadow: 0 0 0 1px silver; cursor: pointer;}
      ⍝ #world table {box-shadow: 0 0 0 1px silver inset;}
      ⍝ #step {width: 3em;}
     
      FILES←¯4↓¨⊃¨↓#.Files.List #.Boot.AppRoot,'Examples/Data/Life/*.txt'
      WORLD←Load INIT
      HISTORY←,⊂⌊WORLD
     
      ('#select'Add _.Select(FILES(FILES⍳⊂INIT)⍬'Select world:')).On'change' 'Select'
      {(⍵'.act')Add _.Button ⍵⊣Add'&nbsp;'}¨'Stop' 'Step' 'Slow' 'Fast'
      ('#step'Add _.Input'number'STEP' Step size: ').On'change' 'Step'
      '#shapes'Add _.RadioButtonGroup((SHAPES,¨⊂'&nbsp;')3 'right' 1)
      Add _.p('Generation '('#gen'New _.span 1))
      '#world'Add _.div(Disp WORLD)
     
      Add _.Timer 50 0 'tick' 'Fast'
      Add _.Timer 250 0 'tick' 'Slow'
      On'tick' 'Advance'
      ⍝ Why doesn't this work?
      Add _.Handler'.act,#shapes_container input' 'click' 'Action' ⍝'' '' '$(function(){Faststop();Slowstop();});'
    ∇

    ∇ r←Select
      :Access public
      WORLD←Load _value
      r←Reset WORLD
    ∇

    Load←{⊃⍪/,/(3 3⍴¯5↑1)×⊂↑'*'=#.Files.GetVTV #.Boot.AppRoot,'Examples/Data/Life/',⍵,'.txt'}

    ∇ r←Edit
      :Access Public
      r←''
      :If 'grid'≢_what
          r,←Stop
          ((⊂⍎¨'\d+'⎕S'&'⊢_what)⊃WORLD)≤←0.5
          r,←Reset WORLD
      :EndIf
    ∇

    ∇ r←Reset world
      r←'#world'Replace Disp world
      HISTORY←,⊂⌊world
      r,←'#gen'Replace 1
    ∇

    ∇ r←Action
      :Access Public
      r←Stop
      :Select _what
      :CaseList 'Slow' 'Fast'
          r,←Go _what
      :Case 'Step'
          RUN←1
          r,←Advance
          RUN←0
      :Case 'Reset'
          WORLD←Load INIT
          r,←Reset WORLD
      :CaseList 'shapes_'∘,¨⍕¨⍳5
          SHAPE←SHAPES⊃⍨⍎⊃⌽_what
      :EndSelect
    ∇

    ∇ r←Go speed
      RUN←1
      r←Execute'$(function(){',_what,'run();});'
    ∇

    ∇ r←Stop
      RUN←0
      r←Execute'$(function(){Faststop();Slowstop();});'
    ∇

    ∇ r←Advance;snap;now;orig
      :Access Public
      r←''
      :For step :In ⍳STEP
          :If RUN
              WORLD←(0.5×⌊WORLD)+Life(⍎SHAPE)⌊WORLD
              r,←'#world'Replace Disp WORLD
              snap←⊂⌊WORLD
              :If snap∊HISTORY
                  r,←Stop
                  now←1+≢HISTORY
                  orig←HISTORY⍳snap
                  r,←'#gen'Replace now' ≡ Generation 'orig' (period is '(now-orig)')'
                  HISTORY←,snap
              :Else
                  HISTORY,←snap
                  r,←'#gen'Replace≢HISTORY
              :EndIf
          :EndIf
      :EndFor
    ∇

    ∇ r←Step
      :Access public
      STEP←1⌈⍎_value∩⎕D
      r←''
    ∇

    ∇ table←Disp world
      table←'#grid'New _.Table((''DEAD LIVE SURVIVOR[1+2×world])'.slot' 0 '' 1)
      table.On'click' 'Edit'
    ∇

    Life←{↑1 ⍵∨.∧3 4=+/,¯1 0 1∘.(⌽∘⍉)⍣2⊂⍵}

      Plane←{                     ⍝ ┌───┐
          1 0↓0 1↓⍺⍺ 0,0⍪⍵        ⍝ │   │
                                  ⍝ └───┘
      }
      Cylinder←{                  ⍝ ┌───┐
          1 0↓⍺⍺ 0⍪⍵              ⍝ ⍋   ⍋
                                  ⍝ └───┘
      }
      Torus←{                     ⍝ ┌─→─┐
          ⍺⍺ ⍵                    ⍝ ⍋   ⍋
                                  ⍝ └─→─┘
      }
      Möbius←{                    ⍝ ┌───┐ möbius strip
          1 0↓0 2↓¯1⌽⍺⍺ 0⍪Twist ⍵ ⍝ ⍋   ⍒
                                  ⍝ └───┘
      }
      Klein←{                     ⍝ ┌─→─┐ klein bottle
          0 2↓¯1⌽⍺⍺ Twist ⍵       ⍝ ⍋   ⍒
                                  ⍝ └─→─┘
      }
      Twist←{                     ⍝  ┌───┐    ┌ ┌───┐ ┐
          ¯1⌽⍵,⌽⊖2↑[1+⎕IO]¯1⌽⍵    ⍝  ⍋   ↑ →  ↓,⍋   ↑,⍒
                                  ⍝  └───┘    └ └───┘ ┘
      }
:EndClass
