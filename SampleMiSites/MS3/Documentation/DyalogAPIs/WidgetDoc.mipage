:Class WidgetDoc : MiPageSample

    :include #.MS3SiteUtils

    ∇ Compose;ns;widget;ref;wref;src;chunk;div;files;r;split;find
      :Access public
      Add _.style'.red {color: red;} #view {display: none}'
      r←''
      (ns widget)←Get'namespace widget'
      div←'#widgetdoc'Add _.div
      :If 9.1=#.⎕NC⊂ns
          ref←#.⍎ns
          :If 9=ref.⎕NC widget
              wref←ref⍎widget
              r,←'<h3 id="WidgetDocTitle">'
              :If 0=wref.⎕NC⊂'DocBase'
              :OrIf 0∊⍴wref.DocBase      ⍝ without link
                  r,←ns,'.',widget
              :Else                      ⍝ with link
                  r,←(ns,'.',widget)External wref.DocBase
              :EndIf
     
              src←1↓⎕SRC wref
              src↓⍨←+/∧\''∘≡¨src
              src←#.Strings.dlb¨src
              src←src/⍨∧\'⍝'=⊃¨src
              src←'¶  +'⎕R' '⊢'¶',¨1↓¨src
              src←{1↓¨⍵⊂⍨'¶'=⍵}∊src
              src←HtmlSafeText¨#.Strings.dlb¨src
     
              :If 0∊⍴src
                  r,←'<h3 class="red">No documentation found.</h3>'
              :Else
                  :For chunk :In src
                      :If ∨/'::'⍷chunk ⍝ section
                          :If ∨/find←' - '⍷chunk
                              split←find⍳1
                              r,←'<h3>',(chunk↑⍨¯1+split),'</h3>'
                              r,←'<p>',(chunk↓⍨2+split),'</p>'
                          :Else
                              r,←'<h3>',chunk,'</h3>'
                          :EndIf
                      :ElseIf '-'=⊃' '~⍨chunk ⍝ text
                          chunk←#.Strings.deb chunk
                          r,←'<p>',('-'~⍨2↑chunk),2↓chunk,'</p>'
                      :ElseIf ∨/find←' - '⍷chunk ⍝ term - definition
                          split←find⍳1
                          r,←'<dl><dt>',(chunk↑⍨¯1+split),'</dt>'
                          chunk←chunk↓⍨2+split
                          r,←'<dd>'
                          :If '&#9053;'≡7↑chunk
                              r,←7↓chunk
                          :Else
                              r,←'<code>',chunk,'</code>'
                          :EndIf
                          r,←'</dd></dl>'
                      :Else ⍝ [APL code] [⍝ comment]
                          find←'&#9053;'⍷chunk
                          split←6+find⍳1
                          r,←'<p><code>',(split↑chunk),'</code>',(split↓chunk),'</p>'
                      :EndIf
                  :EndFor
              :EndIf
     
              files←'relevant'ForControl widget
              :If 0∊⍴files
                  r,←'<h3 class="red">No relevant sample pages found.</h3>'
              :Else
                  r,←'<h3>Relevant Sample Pages:</h3>'
                  (files←'#samples'New _.Tabs(↓⍉↑{DescrEmbed ⍵}¨files)).Theme←'FF8C00'
                  r,←files.Render
              :EndIf
          :Else
              r,←'<h3 class="red">Widget "',widget,'" not found in namespace ',ns,'.</h3>'
          :EndIf
      :Else
          r,←'<h3 class="red">Namespace "',ns,'" not found.</h3>'
      :EndIf
      Add'<h3>Description::' '::'⎕R'' ':'⊢r
    ∇
:EndClass
