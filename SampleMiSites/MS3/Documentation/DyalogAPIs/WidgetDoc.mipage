:Class WidgetDoc : MiPageSample

    :include #.MS3SiteUtils

    ∇ Compose;ns;widget;ref;wref;src;chunk;div;r;dash;files
      :Access public
      Add _.style'.red {color: red;} #view {display: none}'
      (ns widget)←Get'namespace widget'
      div←'#widgetdoc'Add _.div
      r←¯1
      :If 9.1=#.⎕NC⊂ns
          ref←#.⍎ns
          r←¯2
          :If 9=ref.⎕NC widget
              wref←ref⍎widget
              src←1↓⎕SRC wref
              src↓⍨←+/∧\''∘≡¨src
              src←#.Strings.dlb¨src
              src←src/⍨∧\'⍝'=⊃¨src
              src←HtmlSafeText¨#.Strings.dlb¨{1↓¨⍵⊂⍨'¶'=⍵}∊'¶  +'⎕R' '⊢'¶',¨1↓¨src
              r←''
              :For chunk :In src
                  :If ∨/'::'⍷chunk
                      chunk←'::'⎕R':'⊢chunk
                      :If ∨/' - '⍷chunk
                          dash←1⍳⍨' - '⍷chunk
                          r,←'<h3>',(chunk↑⍨¯1+dash),'</h3>'
                          r,←'<p>',(chunk↓⍨2+dash),'</p>'
                      :Else
                          r,←'<h3>',chunk,'</h3>'
                      :EndIf
                  :ElseIf '-'=⊃' '~⍨chunk
                      chunk←#.Strings.deb chunk
                      r,←'<p>',('-'~⍨2↑chunk),2↓chunk,'</p>'
                  :ElseIf ∨/' - '⍷chunk
                      dash←1⍳⍨' - '⍷chunk
                      r,←'<dl><dt>',(chunk↑⍨¯1+dash),'</dt>'
                      r,←'<dd><code>',(chunk↓⍨2+dash),'</code></dd></dl>'
                  :Else
                      r,←'<p><code>',chunk,'</code></p>'
                  :EndIf
              :EndFor
              files←'relevant'ForControl widget
          :EndIf
      :EndIf
     
      :Select r
      :Case ¯1
          '.red'div.Add _.h2('Namespace "',ns,'" not found.')
      :Case ¯2
          '.red'div.Add _.h2('Widget "',widget,'" not found in namespace ',ns,'.')
      :Else
          :If 0≠wref.⎕NC⊂'DocBase'
          :AndIf ~0∊⍴wref.DocBase ⍝ with link
              div.Add'<h2>',(ns,'.',widget)External wref.DocBase
          :Else                   ⍝ without link
              div.Add'<h2>',ns,'.',widget
          :EndIf
     
          :If 0∊⍴r
              '.red'div.Add _.h3'No documentation found.'
          :Else
              div.Add r↓⍨16×'<h3>Description:'≡16↑r
          :EndIf
     
          :If ~0∊⍴files
              div.Add _.h3'Relevant Sample Pages:'
              ('#samples'div.Add _.Tabs(↓⍉↑{DescrEmbed ⍵}¨files)).Theme←'FF8C00'
          :EndIf
     
      :EndSelect
    ∇
:EndClass
