:Class index : MiPageSample ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝
⍝ ┌─────────────────────────────────────────────────────────────────────────────────────────────┐ ⍝
⍝ │ This is the home page of the MiServer Sample Site, MS3, accessible at miserver.dyalog.com   │ ⍝
⍝ └─────────────────────────────────────────────────────────────────────────────────────────────┘ ⍝
⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝ ⍝

    :Include #.MS3SiteUtils

    ∇ Make
      :Access public
      :Implements Constructor
      Cacheable←1
    ∇


    ∇ Compose;cols;tabs;rows;div
      :Access Public
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      (tabs←'bigtabs'Add _.Tabs).Theme←'FF8C00'
      
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Titles,←'Introduction'Over'getting started/recommendations'
      cols←1 0⍴⍬
      cols,←'about'New _.div(Read'IndexData/intro.txt')
      cols,←'slideshow'New _.div(Shuffle Read'IndexData/slideshow.txt')
      cols,←'reading'New _.List(ReadingTree Read'IndexData/reading.txt')
      tabs.Sections,←'intro'New _.Table cols
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Titles,←'Tutorial'Over'visual access to/common controls'
      tabs.Sections,←Tutorial
      ⍝tabs.Sections,←'data="Examples/Techniques/CommonControlsExample.mipage?nowrapper=1"'New _.object
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Titles,←'Samples'Over'pages and mini-apps/each uses just a few controls'
      cols←⍬
      cols,←⊂(⊂'Task demonstrated'),C.tasks
      cols,←⊂(⊂'Type'),C.type
      cols,←⊂(⊂'Main controls used'Over'documentation links'),C.used
      div←New _.div _.br(_.A'Browse Examples Directory' '/Examples/')_.br
      div.Add'eg'New _.DataTable(⍉↑cols)
      tabs.Sections,←div
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Titles,←'Controls'Over'list of all supported controls/links to samples using them'
      cols←⍬
      cols,←⊂('Control'Over'documentation link'),C.doclink
      cols,←⊂(⊂'Description'),C.descr
      cols,←⊂(⊂'Relevant samples'),C.reldocs
      tabs.Sections,←'ctrl'New _.DataTable(¯1 0↓⍉↑cols)
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Titles,←'Reference'Over'general information/technique descriptions'
      rows←⍬
      rows,←'pdf'FormatList'Documentation'
      rows,←FormatList'Examples/Techniques'
      tabs.Sections,←'doc'New _.DataTable('Category' 'Subject'⍪↑rows)
     
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
      tabs.Sections[2 3 4].class←⊂'display'
    ∇

    ∇ tut←Tutorial;tabs;l;r;sp;fs;ig;colors;rgbs;button
      tut←New _.div
      '#p'tut.Add _.p'Here are some common UI elements. Double-click on each one for more information about it.'
      tabs←'#Tabs'tut.Add _.Tabs
      'First tab'tabs.AddSection l←'#div'New _.div
      button←'#Button'l.Add _.Button'Button'
      sp←'#StackPanel' '.nl'l.Add _.StackPanel
      sp.Add _.EditField'EditField' 'EditField'
      '#Select'sp.Add _.Select
      l.Add _.br
      fs←'.Fieldset'l.Add _.Fieldset'CheckBoxs'
      ig←'#InputGrid'fs.Add _.InputGrid('Checked' 'Unchecked')('.CheckBox'∘New¨_.CheckBox,¨'checked' 'unchecked')
      ig.Flip←1
      fs←'.Fieldset'l.Add _.Fieldset'RadioButtonGroup'
      '#RadioButtonGroup'fs.Add _.RadioButtonGroup('Selected' 'Unselected')1 'right'
      '#Input'l.Add _.Input'number' 426 ' Number Input: '
      l.Add _.h5'Progress bar'
      '#progress' 'value="42.6" max="100"'l.Add _.progress
      l.Add _.h5'Slider'
      '#ejSlider'l.Add _.ejSlider(0 99 1)
      ('#ejTreeGrid' '.grid'l.Add _.ejTreeGrid((⍪'Name' 'cell1' 'cell2' 'cell3' 'cell4')(0 1 2 2 1))).width←300
      colors←'aqua' 'black' 'blue' 'fuchsia' 'gray' 'green' 'lime' 'maroon' 'navy' 'olive' 'purple' 'red' 'silver' 'teal' 'white' 'yellow'
      rgbs←'#00FFFF' '#000000' '#0000FF' '#FF00FF' '#808080' '#008000' '#00FF00' '#800000' '#000080' '#808000' '#800080' '#FF0000' '#C0C0C0' '#008080' '#FFFFFF' '#FFFF00'
      'width'('#ejGrid' '.grid'l.Add _.ejGrid(colors,⍪rgbs)('Color' 'RGB')).Set ⍬
      'Second Tab'tabs.AddSection New _.div
      tut.Add _.style ScriptFollows
      ⍝ #Button, .grid {vertical-align: top;}
      ⍝ #StackPanel, .Fieldset, .grid {display: inline-block;}
      ⍝ h5 {margin: 1em 0 0.5em 0;}
      ⍝ #Input {width: 4em;}
      ⍝ #progress {width: 100%;}
      ⍝ .grid {margin-top: 1em;}
      ⍝ #ejTreeGrid {width: 30%; margin-right: 4em;}
      ⍝ #ejGrid {width: 60%;}
      ⍝ .jBox-container {background: black; color: darkorange; font-size: small; border-radius: 10px !important; box-shadow: none !important;}
     
      Interactive¨'#p' '#Tabs' '#div' '#Button' '#StackPanel' '#EditField' '#Select' '.Fieldset' '.CheckBox' '#RadioButtonGroup' '#Input' 'h5' '#progress' '#ejSlider' '#ejTreeGrid' '#ejGrid'
     
    ∇
      Interactive←{
          ctrl←⍵~'#.'
          js←'window.open("',(⍵ #.MS3SiteUtils.DocUrl ctrl),'","_blank");'
          handler←Add _.Handler ⍵'dblclick' 0 '' ''js
          (jb←Add _.jBox('Mouse'('_.',ctrl))).Selector←⍵
          'width' 'adjustTracker'jb.Set 160 'scroll'
      }
:EndClass
